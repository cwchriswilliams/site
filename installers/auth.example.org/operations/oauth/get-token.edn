;; TODO: Rename to exchange-code-for-token
{:install
 {:juxt.site/subject-id "https://auth.example.org/_site/subjects/system"
  :juxt.site/operation-id "https://auth.example.org/_site/operations/create-operation"
  :juxt.site/input
  {:xt/id "{{$id}}"

   :juxt.site.malli/input-schema
   [:map
    ["grant_type" [:enum "authorization_code"]]
    ["code" :string]
    ["client_id" :string]
    ["redirect_uri" :string]
    ["code_verifier" {:optional true} :string]]

   :juxt.site/prepare
   {:juxt.site.sci/program
    #juxt.pprint
    (let [input (:juxt.site/received-representation *ctx*)
          content-type (apply format "%s/%s"
                              ((juxt :juxt.reap.alpha.rfc7231/type
                                     :juxt.reap.alpha.rfc7231/subtype)
                               (:juxt.reap.alpha.rfc7231/content-type input)))

          ;; "The authorization server SHOULD document the size of any
          ;; value it issues." -- RFC 6749 Section 4.2.2
          jti-length 16

          {client-id "client_id" :as form
           code "code"
           code-verifier "code_verifier"}
          (case content-type
            "application/x-www-form-urlencoded"
            (some->
             input
             :juxt.http/body
             (String.)
             ring.util.codec/form-decode
             juxt.site.malli/validate-input))]

      {:client-id client-id
       :code code
       :code-verifier code-verifier
       :jwt-claims {"iss" "https://auth.example.org"
                    "jti" (juxt.site.util/make-nonce jti-length)}})}

   :juxt.site/transact
   {:juxt.site.sci/program
    #juxt.pprint
    (let [{:keys [client-id code code-verifier jwt-claims scopes]} *prepare*
          code-doc (juxt.site/lookup-authorization-code code)
          verified?
          juxt.site/verify-authorization-code
          {:code-verifier code-verifier
           :code-challenge (:juxt.site/code-challenge code-doc)
           :code-challenge-method (:juxt.site/code-challenge-method code-doc)}]

      (if verified?
        (let [{:juxt.site/keys [origin resource-server]}
              (juxt.site/lookup-client client-id)

              _ (assert origin)
              _ (assert resource-server)

              jwt-id (get jwt-claims "jti")

              access-token
              (juxt.site/make-jwt
               (merge
                jwt-claims
                {"aud" resource-server
                 "iat" (java.util.Date.)
                 "nbf" (let [now (.toInstant (java.util.Date.))
                             expiry (.minus now 2 java.time.temporal.ChronoUnit/MINUTES)]
                         (java.util.Date/from expiry))
                 "exp" (let [now (.toInstant (java.util.Date.))
                             expiry (.plus now 15 java.time.temporal.ChronoUnit/MINUTES)]
                         (java.util.Date/from expiry))}))

              access-token-doc
              (cond-> {:xt/id (str "https://auth.example.org/access-tokens/" jwt-id)
                       :juxt.site/type "https://meta.juxt.site/types/access-token"
                       :juxt.site/subject (:juxt.site/subject code-doc)
                       :juxt.site/application (:juxt.site/application code-doc)
                       :juxt.site/token access-token}
                scopes (assoc :juxt.site/scope scopes))]

          [[:xtdb.api/put access-token-doc]
           [:ring.response/status 201]
           [:ring.response/headers
            {"content-type" "application/json"
             "access-control-allow-origin" origin
             "access-control-allow-credentials" "true"}]
           [:ring.response/body
            (jsonista.core/write-value-as-string {"access_token" access-token})]])

        ;; TODO: Catch all exceptions too and implement appropriate
        ;; error handling
        (throw (ex-info "TODO: create and return error" {}))))}

   :juxt.site/rules
   [
    [(allowed? subject operation resource permission)
     [subject :juxt.site/user-identity id]
     [id :juxt.site/user user]
     [permission :juxt.site/user user]]]}}}
